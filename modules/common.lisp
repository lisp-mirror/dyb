(in-package :dyb)

(defun ensure-string-reply (reply)
  (etypecase reply
    (string reply)
    ((vector (unsigned-byte 8)) (babel:octets-to-string reply))
    (null nil)))

(defun handle-endpoint (user request &key error-path)
  (let ((result)
        (message))
    (cond ((not (get-val user 'last-access-token))
           (setf message "Missing access token"))
          ((not request)
           (setf message "Endpoint returned no values."))
          (t
           (setf result (json:decode-json-from-string
                         (ensure-string-reply request)))
           (when (and (consp result)
                      (or (assoc-path result error-path) 
                          (assoc-path result :error) 
                          (assoc-path result :errors)))
             (let ((error-message (or (assoc-path result error-path)
                                      (assoc-path result :error :message)
                                      (if (listp (cdr (assoc-path result :errors)))
                                          (assoc-path (car (cdr (assoc-path result :errors))) :message)
                                          (assoc-path result :errors)))))
               (setf message (if (listp error-message)
                                 (cdr error-message)
                                 error-message))))))
    (values result message)))

(defun handle-endpoint-run-request (user request-function &key error-path)
  (let ((result)
        (message))
    (if (get-val user 'last-access-token)
        (multiple-value-bind (body status header uri stream must-close reason-phrase)
            (funcall request-function)
          (declare (ignore header uri stream must-close))
          (cond (body
                 (setf result (json:decode-json-from-string
                               (ensure-string-reply body)))
                 (when (or (assoc-path result error-path) 
                           (assoc-path result :error) 
                           (assoc-path result :errors))
                   (setf message (cdr (or (assoc-path result error-path)
                                          (assoc-path result :error :message)
                                          (assoc-path result :errors :message))))))
                ((or (eql status 200) (eql status 201))
                 (setf result reason-phrase))
                (t
                 (setf message "Endpoint returned no values."))))
        (setf message "Missing access token"))
    (values result message)))
